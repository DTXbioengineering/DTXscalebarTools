// Code for "BatchProcessFolders" follows, with processFile() edited.

// "BatchProcessFolders"
//
// This macro batch processes all the files in a folder and any
// subfolders in that folder. In this example, it runs the Subtract 
// Background command of TIFF files. For other kinds of processing,
// edit the processFile() function at the end of this macro.

   requires("1.33s"); 
   dir = getDirectory("Choose a Directory ");
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);
   //print(count+" files processed");
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  function processFile(path) {
       if (endsWith(path, ".tif")) {
           open(path);
		//Start editing here
          	run("Set Scale...", "distance=0 known=0 pixel=1 unit=pixel");
		setAutoThreshold("Default");
		//run("Threshold...");
		setAutoThreshold("Default dark");
		setThreshold(62, 255);
		run("Convert to Mask");
		run("Analyze Particles...", "size=14-400 circularity=0.00-1.00 show=Outlines display clear summarize");
		//Stop editing here
           	//save(path);
           	close();
      }
  }

